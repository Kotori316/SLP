buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
plugins {
    id 'scala'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}
apply plugin: 'net.minecraftforge.gradle'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = getProperty("scala_version")
group = "com.kotori316" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Scala-library"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileScala {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    mappings channel: 'snapshot', version: '20180921-1.13'
    runs {
        client {
            workingDirectory project.file('Minecraft')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            mods {
                "scala-library" {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run-server')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            mods {
                "scala-library" {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    def scala_version = getProperty("scala_version")
    def scala_major = getProperty("scala_major")
    def java8_compat_version = getProperty("java8_compat_version")

    minecraft 'net.minecraftforge:forge:1.13.2-25.0.191'
    implementation "org.scala-lang:scala-library:${scala_version}"
//    implementation "org.scala-lang.modules:scala-java8-compat_${scala_major}:${java8_compat_version}"
    
    // https://mvnrepository.com/artifact/org.typelevel/cats-core
    implementation group: 'org.typelevel', name: "cats-core_${scala_major}", version: '1.6.0'

}

jar {
    def mainClassName = "com.kotori316.scala_lib.Main"
    manifest {
        attributes([
                "Specification-Title"     : project.name,
                "Specification-Vendor"    : "Kotori316",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "Kotori316",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Main-Class'              : mainClassName
        ])
    }
}

// Tell the artifact system about our extra jars
artifacts {
    archives shadowJar
}

shadowJar {
    def groupNames = ['org.scala-lang', 'org.scala-lang.modules', 'org.typelevel']
    classifier = "with-library"
    dependencies {
        include(dependency {
            groupNames.contains(it.moduleGroup)
        })
    }
}
