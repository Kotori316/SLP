plugins {
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.8"
    id "com.github.breadmoirai.github-release" version "2.5.2"
}

tasks.named("wrapper", Wrapper.class) {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = "8.7"
}

version = "${libs.versions.scala3.get()}-build-${project.build_number}"

final boolean RELEASE_DEBUG = Boolean.parseBoolean(System.getenv("RELEASE_DEBUG") ?: "true")
githubRelease {
    setOwner("Kotori316")
    repo.set("SLP")
    token = project.findProperty("githubToken") ?: System.getenv("REPO_TOKEN") ?: ""
    targetCommitish.set(project.property("branch") as String)
    prerelease.set(project.version.toString().contains("SNAPSHOT"))
    body.set("""\
        For Minecraft ${libs.versions.minecraft.get()}
        
        This mod provides language provider, "kotori_scala".
        
        Scala3: ${libs.versions.scala3.get()}
        Scala: ${libs.versions.scala2.get()}
        Cats: ${libs.versions.cats.get()}
        """.stripIndent())
    releaseAssets = files(
            fileTree(findProject(":forge")?.layout?.buildDirectory?.dir("libs")) {
                include("*.jar")
            },
            fileTree(findProject(":neoforge")?.layout?.buildDirectory?.dir("libs")) {
                include("*.jar")
            }
    )
    dryRun.set(RELEASE_DEBUG)
}
