buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = 'https://repo.spongepowered.org/maven' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
plugins {
    id 'scala'
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


def scala_version = getProperty("scala_version")
def build_number = getProperty("build_number")
version = "${scala_version}-build-${build_number}"
group = "com.kotori316" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ScalableCatsForce"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileScala {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    mappings channel: 'snapshot', version: '20201028-1.16.3'
    runs {
        client {
            workingDirectory project.file('Minecraft')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'
            property "mixin.debug.export", "true"

            mods {
                "scala-library-object" {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run-server')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            mods {
                "scala-library-object" {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run-server')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
            forceExit false

            if (Boolean.valueOf(System.getenv("GITHUB_ACTIONS")))
                args '--mod', "slp_test", '--dev'
            else
                args '--mod', "slp_test", '--all'

            mods {
                "slp_test" {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        name = "Mixin"
        url = 'https://repo.spongepowered.org/maven'
    }
}

private static String forgeVersion() {
    String CI_FORGE = System.getenv("CI_FORGE")
    if (CI_FORGE != null && CI_FORGE != "fixed") {
        return "net.minecraftforge:forge:" + CI_FORGE
    } else {
        return 'net.minecraftforge:forge:1.16.4-35.0.7'
    }
}

def mainClassName = "com.kotori316.scala_lib.Main"
dependencies {
    def scala_major = getProperty("scala_major")
    def java8_compat_version = getProperty("java8_compat_version")
    def cats_version = getProperty("cats_version")

    minecraft(forgeVersion())
    implementation "org.scala-lang:scala-library:${scala_version}"
//    implementation "org.scala-lang.modules:scala-java8-compat_${scala_major}:${java8_compat_version}"

    // https://mvnrepository.com/artifact/org.typelevel/cats-core
    implementation group: 'org.typelevel', name: "cats-core_${scala_major}", version: cats_version

    // Test Dependencies.
    implementation(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.0')
    runtimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.0')
    implementation(group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.7.0')

    // Mixin
    annotationProcessor("org.spongepowered:mixin:0.8:processor")
}

test {
    useJUnitPlatform()
}
mixin {
    add sourceSets.main, "mixins.slp.refmap.json"
}

jar {
    manifest {
        attributes([
                'FMLModType'  : 'LANGPROVIDER',
                "MixinConfigs": "slp.mixins.json"
        ])
        attributes([
                "Specification-Title"     : project.name,
                "Specification-Vendor"    : "Kotori316",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "Kotori316",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Main-Class'              : mainClassName
        ])
    }
    archiveClassifier.set('dev')
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

// Tell the artifact system about our extra jars
artifacts {
    archives shadowJar as Object, srcJar
}

shadowJar {
    def groupNames = ['org.scala-lang', 'org.scala-lang.modules', 'org.typelevel']
    archiveClassifier.set("with-library")
    dependencies {
        include(dependency {
            groupNames.contains(it.moduleGroup)
        })
    }

}

publishing {
    repositories {
        maven {
            name = "BuildDir"
            // change to point to your repo.
            url = "$buildDir/repo"
        }
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Kotori316/SLP")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR") ?: ""
                password = project.findProperty("githubToken") ?: System.getenv("REPO_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName.toLowerCase()
            artifact srcJar
            artifact jar
            pom {
                name = archivesBaseName
                description = 'Scala Loading library for Minecraft'
                url = 'https://github.com/Kotori316/SLP'
                packaging = "jar"
                withXml {
                    asNode().children().last() + {
                        Project p = getProject()
                        resolveStrategy = Closure.DELEGATE_FIRST
                        dependencies {
                            p.configurations.implementation.dependencies.each { Dependency dep ->
                                if (!dep.name.contains("junit")) {
                                    dependency {
                                        groupId dep.group
                                        artifactId dep.name
                                        version dep.version
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

task data() {
    doLast {
        println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') +
                '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
        println("Forge: ${forgeVersion()}")
        println("All Dependencies")
        getProject().getConfigurations().forEach { con ->
            if (!con.dependencies.isEmpty()) {
                println("*" * 20)
                println(con.name)
                for (Object o : con.dependencies) {
                    println(o)
                }
            }
        }
        print("\n" * 2)
        println("Dependencies to be included in pom")
        getProject().getConfigurations().implementation.dependencies.each { Dependency dep ->
            if (!dep.name.contains("junit")) {
                printf("Group: %s, ArtifactId: %s, Version: %s%n", dep.group, dep.name, dep.version)
            }
        }
    }
}
