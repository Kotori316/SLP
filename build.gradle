import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://maven.minecraftforge.net/" }
        maven { url = 'https://maven.parchmentmc.org' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
    }
}
plugins {
    id 'scala'
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

def scala_version = String.valueOf(getProperty("scala_version"))
def build_number = String.valueOf(getProperty("build_number"))
version = "${scala_version}-build-${build_number}"
group = "com.kotori316" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ScalableCatsForce"

java.toolchain.languageVersion = JavaLanguageVersion.of(System.getenv("CI_JAVA") ?: "17")

compileScala {
    scalaCompileOptions.additionalParameters = ["-Wconf:cat=deprecation:w,any:e"]
}

minecraft {
    String parchmentVersion = System.getenv("CI_PARCHMENT") ?: project.parchmentMapping
    if (parchmentVersion.equalsIgnoreCase("official")) {
        mappings channel: 'official', version: minecraftVersion()
    } else {
        if (parchmentVersion.equalsIgnoreCase("default")) {
            mappings channel: 'parchment', version: project.parchmentMapping
        } else {
            mappings channel: 'parchment', version: parchmentVersion
        }
    }
    runs {
        client {
            workingDirectory project.file('Minecraft')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'
            property "mixin.debug", "true"

            mods {
                "scala-library-object" {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run-server')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            mods {
                "scala-library-object" {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run-server')
            property 'forge.logging.markers', 'REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            forceExit false

            if (Boolean.valueOf(System.getenv("GITHUB_ACTIONS")))
                args '--mod', "slp_test,scala-library-object,scala-library-java", '--dev'
            else
                args '--mod', "slp_test", '--all'

            mods {
                "slp_test" {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets {
    main {
        scala {
            srcDir 'src/main/scala'
            exclude "/com/kotori316/scala_lib/asm/**"
            if (System.getenv("CI") == null) {
                // I use classes in package `example` in tests.
                exclude "/com/kotori316/scala_lib/example/**"
                exclude "/com/kotori316/scala_lib/config/**"
            }
        }
    }
    test {
        scala {
            if (System.getenv("CI") == null) {
                exclude "/com/kotori316/scala_lib/config/**"
            }
        }
    }
}

private static String forgeVersion() {
    String CI_FORGE = System.getenv("CI_FORGE")
    if (CI_FORGE != null && CI_FORGE != "fixed") {
        return "net.minecraftforge:forge:" + CI_FORGE
    } else {
        return "net.minecraftforge:forge:1.18.1-39.0.88"
    }
}

private static String minecraftVersion() {
    String v = forgeVersion()
    return v.substring(v.lastIndexOf(":") + 1, v.lastIndexOf("-"))
}

def mainClassName = "com.kotori316.scala_lib.Main"
repositories {
    mavenLocal()
    maven {
        name = "Azure-SLP"
        url = uri("https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1")
        content {
            it.includeVersion("org.typelevel", "cats-core_2.13", String.valueOf(project.cats_version))
            it.includeVersion("org.typelevel", "cats-kernel_2.13", String.valueOf(project.cats_version))
        }
    }
}

dependencies {
    String scala_major = scala_version.substring(0, scala_version.lastIndexOf("."))
    String cats_version = getProperty("cats_version")

    minecraft(forgeVersion())
    // https://mvnrepository.com/artifact/org.scala-lang/scala-library
    implementation(group: 'org.scala-lang', name: 'scala-library', version: scala_version)
    // https://mvnrepository.com/artifact/org.typelevel/cats-core
    implementation(group: 'org.typelevel', name: "cats-core_" + scala_major, version: cats_version)

    // Test Dependencies.
    implementation(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.1')
    testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.7.1')
    runtimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.1')
    implementation(group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.7.0')

}

test {
    useJUnitPlatform()

    testLogging {
        events "skipped", "failed", "standardOut", "standardError"
        exceptionFormat 'full'
    }
}

jar {
    manifest {
        attributes([
                'FMLModType'           : 'LANGPROVIDER',
                'Automatic-Module-Name': 'kotori_scala',
        ])
        attributes([
                "Specification-Title"     : project.name,
                "Specification-Vendor"    : "Kotori316",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "Kotori316",
                "Implementation-Timestamp": ZonedDateTime.now().format(DateTimeFormatter.ISO_INSTANT),
                'Main-Class'              : mainClassName
        ])
    }
    archiveClassifier.set('dev')
}

task normalJar(type: Jar) {
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// Tell the artifact system about our extra jars
artifacts {
    archives shadowJar as Object, srcJar
}

shadowJar {
    def groupNames = ['org.scala-lang', 'org.scala-lang.modules', 'org.typelevel']
    archiveClassifier.set("with-library")
    dependencies {
        include(dependency {
            groupNames.contains(it.moduleGroup)
        })
    }

}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Kotori316/SLP")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR") ?: ""
                password = project.findProperty("githubToken") ?: System.getenv("REPO_TOKEN")
            }
        }
        maven {
            name = "AzureRepository"
            url = uri("https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1")
            credentials {
                username = project.findProperty("azureUserName") ?: System.getenv("AZURE_USER_NAME") ?: ""
                password = project.findProperty("azureToken") ?: System.getenv("AZURE_TOKEN") ?: "TOKEN"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName.toLowerCase()
            artifact srcJar
            artifact jar
            artifact shadowJar
            artifact normalJar
            pom {
                name = archivesBaseName
                description = "Scala Loading library build with Minecraft ${minecraftVersion()} and Forge ${forgeVersion()}"
                url = 'https://github.com/Kotori316/SLP'
                packaging = "jar"
                withXml {
                    asNode().children().last() + {
                        Project p = getProject()
                        resolveStrategy = Closure.DELEGATE_FIRST
                        dependencies {
                            p.configurations.implementation.dependencies.each { Dependency dep ->
                                if (!dep.name.contains("junit")) {
                                    dependency {
                                        groupId dep.group
                                        artifactId dep.name
                                        version dep.version
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

static void start_group(String name) {
    println("::group::${name}")
}

@SuppressWarnings('SpellCheckingInspection')
static void end_group() {
    println("::endgroup::")
}

task data() {
    doLast {
        println("Java: ${System.getProperty('java.version')} JVM: ${System.getProperty('java.vm.version')}" +
                "(${System.getProperty('java.vendor')}) Arch: ${System.getProperty('os.arch')}")
        println("Scala: $scala_version, ${scala_version.getClass()}")
        println("Build Number: $build_number, ${build_number.getClass()}")
        println("Version: $version, ${version.getClass()}")
        println("Scala Loading library build with Minecraft ${minecraftVersion()} and Forge ${forgeVersion()}")
        println("Forge: ${forgeVersion()}")
        println("Minecraft: ${minecraftVersion()}")
        print(System.lineSeparator() * 2)
        println("All Dependencies")
        getProject().getConfigurations().forEach { con ->
            if (!con.dependencies.isEmpty()) {
                start_group(con.name)
                for (Object o : con.dependencies) {
                    println(o)
                }
                end_group()
            }
        }
        print(System.lineSeparator() * 2)
        start_group("Dependencies to be included in pom")
        getProject().getConfigurations().implementation.dependencies.each { Dependency dep ->
            if (!dep.name.contains("junit")) {
                printf("Group: %s, ArtifactId: %s, Version: %s%n", dep.group, dep.name, dep.version)
            } else {
                printf("JUNIT - Group: %s, ArtifactId: %s, Version: %s%n", dep.group, dep.name, dep.version)
            }
        }
        end_group()
        start_group("Publishing Information")
        publishing.properties.each { Map.Entry<?, ?> entry ->
            printf("%s -> %s (key: %s, value: %s)%n", entry.key, entry.value, entry.getKey().getClass(), entry.getValue().getClass())
        }
        end_group()
        start_group("Repositories")
//        publishing.properties.get("repositories").getClass().getMethods().each { println(it) }
//        publishing.properties.get("repositories").getClass().getInterfaces().each { println(it) }
        publishing.properties.get("repositories").getAsMap().each { Map.Entry<String, Object> e -> printf("%s=%s%n", e.key, e.value.getUrl()) }
//        (publishing.properties.get("repositories") as Collection<Object>).toArray()[0].getClass().getMethods().each {println(it)}
        end_group()
    }
}
